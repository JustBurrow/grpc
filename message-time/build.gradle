apply plugin: 'com.google.protobuf'

dependencies {
  compile 'com.google.protobuf:protobuf-java'
  compile 'io.grpc:grpc-protobuf'
  compile 'io.grpc:grpc-stub'
  if (JavaVersion.current().isJava9Compatible()) {
    compile 'javax.annotation:javax.annotation-api'
  }
}

sourceSets {
  main {
    proto {
      srcDir 'src/main/proto'
    }

    java {
      srcDir "${protobuf.generatedFilesBaseDir}/main/grpc"
      srcDir "${protobuf.generatedFilesBaseDir}/main/java"
    }
  }

  test {
    proto {
      srcDir 'src/test/proto'
    }
  }
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc"
  }

  plugins {
    grpc { artifact = "io.grpc:protoc-gen-grpc-java" }
  }

  clean {
    delete generatedFilesBaseDir
  }

  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

idea {
  module {
    sourceDirs += file("${projectDir}/src/main/proto")
  }
}